#!/usr/bin/perl

# Just a script to create manochrome manpage.
# To create your own man pages use pod2man.

use strict;
use warnings;
use File::Copy;
use File::Basename;
use Pod::Man;
use Getopt::Std;

my %opts;
getopts('f', \%opts);

my $cwd = dirname(__FILE__);
my $cstring="MANochrome";
my $nstring="manochrome";
my $pod_outfile = "${cwd}/doc/pod/${nstring}.pod";
my $readme="$cwd/Readme.pod";
my $mandir="$cwd/doc/man/man1";
my $infile="$cwd/doc/pod/${nstring}.pod";
my $outfile="$mandir/${nstring}.1";
my $copyoutfile="$mandir/manopod2html.1";
my $date=`date "+%Y-%m-%d"`;
chomp($date);
my $version="v0.1";

die "Could not find $mandir.\n" if (! -d $mandir);
die "Could not find $readme.\n" if (! -e $readme);
die "$infile does not exist.\n" if (! -e $infile);

copy($readme, $pod_outfile);

# Filter HTML from .pod if $opts{f}.
# The HTML is only for github in Readme.pod.
filter_html($pod_outfile) if ($opts{f});

# Convert pod to man.
my $parser = Pod::Man->new(
    center      => $cstring,
    release     => $version,
    section     => 1,
    name        => $nstring,
    date        => $date
);
$parser->parse_from_file($infile, $outfile);

# Create copy of manochrome.1
# man pages for manochrome and manopod2html are identical.
copy($outfile, $copyoutfile);

die "$outfile does not exist" if (! -e $outfile);
die "$copyoutfile does not exist" if (! -e $copyoutfile);

print <<END;
Created $outfile
Created $copyoutfile
END

sub filter_html {
    my ($file) = @_;
    my $filter = 0;

    open(RF, "<$file") or die $!;
    my @rf = <RF>;
    close(RF);

    open(EDIT, ">$file") or die $!;

    for (@rf) {
        if ($_ =~ m/^=begin html/) {
            $filter = 1;
            next;
        }

        if ($_ =~ m/^=end html/) {
            $filter = 0;
            next;
        }

        next if $filter;

        print EDIT $_;
    }

    close(EDIT);
}
